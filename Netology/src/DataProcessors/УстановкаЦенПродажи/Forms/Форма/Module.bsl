
&НаСервере
Процедура ВариантУстановкиЦеныНаСервере()
	
	НоменклатуракОбработке = НоменклатураКОбработке();
	
	Если ВариантУстановки = 0 Тогда 
		УстановитьЦеныНаСервере(НоменклатуракОбработке)
	ИначеЕсли ВариантУстановки = 1 Тогда 
		УдалитьЦеныНаСервере(НоменклатуракОбработке);
	ИначеЕсли ВариантУстановки = 2 Тогда 
		УстановитьПроцентОтЦеныНаСервере(НоменклатуракОбработке)
	КонецЕсли;
				
КонецПроцедуры

&НаСервере
Процедура УстановитьЦеныНаСервере(НоменклатуракОбработке)
	
	Для Каждого Строка из НоменклатуракОбработке Цикл
		ДобавитьЗаписьНаСервере(Строка.Ссылка, ФиксированнаяЦена);	
	КонецЦикла;
				
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаписьНаСервере(Номенклатура, Цена)
	
	МенеджерЗаписи = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьМенеджерЗаписи();	
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.Номенклатура = Номенклатура;
	МенеджерЗаписи.Цена = Цена;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЦеныНаСервере(НоменклатуракОбработке)
	
	Для Каждого Строка из НоменклатуракОбработке Цикл
		НаборЗаписей = РегистрыСведений.ЦеныПродажиНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Номенклатура.Установить(Строка.Ссылка);
		НаборЗаписей.Записать();
	КонецЦикла;
				
КонецПроцедуры

&НаСервере
Процедура УстановитьПроцентОтЦеныНаСервере(НоменклатуракОбработке)
	Для Каждого Строка из НоменклатуракОбработке Цикл
		ПоследняяЦена = ПолучитьПоследнююЦену(Строка);
		Для Каждого СтрокаНоменклатуры  Из ПоследняяЦена Цикл
			ЦенаСПроцентом = СтрокаНоменклатуры.Цена + СтрокаНоменклатуры.Цена * ПроцентОтТекущейЦены / 100;
		КонецЦикла;
	    ДобавитьЗаписьНаСервере(СтрокаНоменклатуры.Номенклатура, ЦенаСПроцентом);
	КонецЦикла;
					
КонецПроцедуры

&НаСервере
Функция ПолучитьПоследнююЦену(СтрокаНоменклатуры)
	
	Номенклатура = СтрокаНоменклатуры.Ссылка;
	Отбор = Новый Структура("Номенклатура", Номенклатура); 
	ПоследняяЦена = РегистрыСведений.ЦеныПродажиНоменклатуры.СрезПоследних(ДатаАктуальности, Отбор);
	
	Возврат ПоследняяЦена;
		
КонецФункции

&НаСервере
Функция НоменклатураКОбработке()
	
	НоменклатуракОбработке = Новый Массив;
	
	Для Каждого Строка Из СписокНоменклатуры Цикл
		Если Строка.Номенклатура.ЭтоГруппа Тогда
			ДополнитьМассивЭлементамиВложеннымиВГруппу(Строка.Номенклатура, НоменклатуракОбработке);
		Иначе
			НоменклатуракОбработке.Добавить(Строка.Номенклатура);
		КонецЕсли;
	КонецЦикла;
	
	Возврат НоменклатуракОбработке;
	
КонецФункции 

&НаСервере
Процедура ДополнитьМассивЭлементамиВложеннымиВГруппу(ГруппаНоменклатуры, НоменклатуракОбработке)

	Выборка = Справочники.Номенклатура.Выбрать(ГруппаНоменклатуры);
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа Тогда                                                     
			ДополнитьМассивЭлементамиВложеннымиВГруппу(Выборка.Ссылка, НоменклатуракОбработке); 
		Иначе
			НоменклатуракОбработке.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Установить(Команда) 
		
	 ВариантУстановкиЦеныНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантУстановкиПриИзменении(Элемент)
	
	Элементы.ФиксированнаяЦена.Доступность = (ВариантУстановки = 0);
	Элементы.ПроцентОтТекущейЦены.Доступность = (ВариантУстановки = 2);
	Элементы.ДатаАктуальности.Доступность = (ВариантУстановки = 2);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Элементы.ПроцентОтТекущейЦены.Доступность = (ВариантУстановки = 2);
	ДатаАктуальности = ТекущаяДата();
	Элементы.ДатаАктуальности.Доступность = (ВариантУстановки = 2);
	
КонецПроцедуры	
	
&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",Ложь);
 	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Элементы.СписокНоменклатуры);

КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыбранныеДанные Из ВыбранноеЗначение Цикл
		СтрокаТаблицы = СписокНоменклатуры.Добавить();
		СтрокаТаблицы.Номенклатура = ВыбранныеДанные;
    КонецЦикла;
	
	КонецПроцедуры
