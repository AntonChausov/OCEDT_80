Функция НайтиИлиСоздатьОтборВСписке(Список, Поле) Экспорт
	
	НайденныйЭлементОтбора = Неопределено;
	ЭлементыОтбора = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		Если ЭлементОтбора.ЛевоеЗначение = Поле Тогда
			НайденныйЭлементОтбора = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если НайденныйЭлементОтбора = Неопределено Тогда
		НайденныйЭлементОтбора = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НайденныйЭлементОтбора.ЛевоеЗначение = Поле;
	КонецЕсли;
	
	Возврат НайденныйЭлементОтбора; 
	
КонецФункции

Процедура УстановитьОтборВСписке(СписокДляУстановкиОтбора, ПолеОтбора, ЗначениеОтбора) Экспорт

	НайденныйЭлементОтбора = НайтиИлиСоздатьОтборВСписке(СписокДляУстановкиОтбора, ПолеОтбора);
	НайденныйЭлементОтбора.Использование = Истина;
	НайденныйЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	НайденныйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	НайденныйЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;

КонецПроцедуры