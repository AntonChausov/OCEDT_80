
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ПересчитатьСуммуСтроки()
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ПересчитатьСуммуСтроки()
	
КонецПроцедуры 

&НаКлиенте
Процедура ПересчитатьСуммуСтроки()
	
	ТекущиеДанныеТаблицы = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанныеТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущиеДанныеТаблицы.Сумма = ТекущиеДанныеТаблицы.Количество * ТекущиеДанныеТаблицы.Цена;

КонецПроцедуры

&НаКлиенте
Процедура СортироватьПоСумме(Команда)
	
	Если СортироватьПоВозрастанию Тогда
		Объект.Товары.Сортировать("Сумма");
	    Элементы.ТоварыСортироватьПоСумме.Заголовок = "Сортировать по возрастанию";
		Элементы.ТоварыСортироватьПоСумме.Картинка = БиблиотекаКартинок.ОформлениеТреугольникВнизКрасный;
	Иначе
		Объект.Товары.Сортировать("Сумма Убыв");
		Элементы.ТоварыСортироватьПоСумме.Заголовок = "Сортировать по убыванию";
		Элементы.ТоварыСортироватьПоСумме.Картинка = БиблиотекаКартинок.ОформлениеТреугольникВверхЗеленый;
	КонецЕсли;
	СортироватьПоВозрастанию = НЕ СортироватьПоВозрастанию;
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВыбораСписком", , Элементы.Товары);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыбранныеДанные Из ВыбранноеЗначение Цикл
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("Номенклатура", ВыбранныеДанные.Номенклатура);
		СтрокиСНоменклатурой = Объект.Товары.НайтиСтроки(ОтборСтрок);
		Если СтрокиСНоменклатурой.Количество() = 0 Тогда
			СтрокаТаблицы = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыбранныеДанные);
		Иначе
			СтрокаСНоменклатурой = СтрокиСНоменклатурой[0];
		    СтрокаСНоменклатурой.Количество = СтрокаСНоменклатурой.Количество + ВыбранныеДанные.Количество;
		    СтрокаСНоменклатурой.Сумма = СтрокаСНоменклатурой.Количество * СтрокаСНоменклатурой.Цена;
		КонецЕсли;
    КонецЦикла;
	
КонецПроцедуры

