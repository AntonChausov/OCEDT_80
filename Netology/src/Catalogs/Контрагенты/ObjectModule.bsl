
Функция ТолькоЦифрыВСтроке(Знач ПроверяемаяСтрока)
	
	Цифры = "0123456789";
	Для Счетчик = 1 По СтрДлина(ПроверяемаяСтрока) Цикл
		Символ = Сред(ПроверяемаяСтрока, Счетчик, 1);
		Если СтрНайти(Цифры, Символ) = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
	
КонецФункции

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
    Сообщить("Обработка проверки заполнения: " + КонтекстВыполненияКлиентСервер.ТекущийКонтекст());
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		
		//Проверка ИНН для юридического лица
		Если НЕ ЗначениеЗаполнено(ИНН) ИЛИ СтрДлина(ИНН) <> 10 Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'для юридического лица ИНН должен быть заполнен и состоять из 10 знаков'");
			Сообщить(ТекстСообщения);
		ИначеЕсли НЕ ТолькоЦифрыВСтроке(ИНН) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'ИНН должен состоять только из цифр'");
			Сообщить(ТекстСообщения);
		ИначеЕсли НЕ РаботаСИНН.ИННВерен(ИНН) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Контрольная сумма ИНН не совпадает, проверьте правильность ввода'");
			Сообщить(ТекстСообщения);
		КонецЕсли;
		
		// Проверка КПП для юридического лица
		Если НЕ ЗначениеЗаполнено(КПП) ИЛИ СтрДлина(КПП) <> 9 Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'для юридического лица КПП должен быть заполнен и состоять из 9 знаков'");
			Сообщить(ТекстСообщения);
		ИначеЕсли НЕ ТолькоЦифрыВСтроке(КПП) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'КПП должен состоять только из цифр'");
			Сообщить(ТекстСообщения);
		КонецЕсли;
		
	Иначе
		
		//Проверка ИНН для Физического лица
		Если НЕ ЗначениеЗаполнено(ИНН) ИЛИ СтрДлина(ИНН) <> 12 Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'для физического лица ИНН должен быть заполнен и состоять из 12 знаков'");
			Сообщить(ТекстСообщения);
		ИначеЕсли НЕ ТолькоЦифрыВСтроке(ИНН) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'ИНН должен состоять только из цифр'");
			Сообщить(ТекстСообщения);	
		КонецЕсли;
		
		// Проверка КПП для Физического лица
		Если ЗначениеЗаполнено(КПП) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'для физического лица КПП должен быть не заполнен'");
			Сообщить(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;

				
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	Ответственный = ПараметрыСеанса.СотрудникТекущегоПользователя; 

КонецПроцедуры




